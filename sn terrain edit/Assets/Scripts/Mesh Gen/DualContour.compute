#pragma kernel DualContour
#include "DualTables.compute"

// MUST be equal to Globals.threadGroupSize
static const int numthreads = 8;

struct Triangle {
    float3 vertexA;
    float3 vertexB;
    float3 vertexC;
    int type;
    
    void Swap(bool clockwise) {
        if (clockwise) {
            float3 temp = vertexA;
            vertexA = vertexC;
            vertexC = temp;
        }
    }
};
struct Face {
    float3 vertexA;
    float3 vertexB;
    float3 vertexC;
    float3 vertexD;
    float3 surfaceIntersection;
    int type;
};

// Fill beforehand, source data
RWStructuredBuffer<int> density;
RWStructuredBuffer<int> pointTypes;

// Output
AppendStructuredBuffer<Face> faces;

int numPointsX;
int numPointsY;
int numPointsZ;
float3 meshOffset;

int linearIndex(uint x, uint y, uint z) {
    return z * numPointsX * numPointsY + y * numPointsX + x;
}

bool checkBoundsAll(int3 pos) {
    return pos.x >= 0 && pos.y >= 0 && pos.z >= 0 && pos.x < numPointsX && pos.y < numPointsY && pos.z < numPointsZ;
}
bool checkBoundsOuterMesh(int3 pos) {
    return pos.x > 0 && pos.y > 0 && pos.z > 0 && pos.x < 35 && pos.y < 35 && pos.z < 35;
}

Face MakeFace(int3 p1, int3 p2, int d1, int d2, int dir, int type) {
    Face face;
    face.type = type;

    // For each edge that exhibits a sign change,
    // generate a quad connecting the minimizing vertices of the four cubes containing the edge.

    int3 cube1Index, cube2Index, cube3Index, cube4Index; 

    int3 pMin = int3(min(p1.x, p2.x), min(p1.y, p2.y), min(p1.z, p2.z));
    
    switch(dir) {
        default:
        case 0:
            // p1->p2 = y+
            cube1Index = int3(pMin.x - 1, pMin.y, pMin.z - 1);
            cube2Index = int3(pMin.x - 1, pMin.y, pMin.z);
            cube3Index = int3(pMin.x, pMin.y, pMin.z);
            cube4Index = int3(pMin.x, pMin.y, pMin.z - 1);
            break;
        case 1:
            // p1->p2 = y-
            cube1Index = int3(pMin.x - 1, pMin.y, pMin.z - 1);
            cube2Index = int3(pMin.x, pMin.y, pMin.z - 1);
            cube3Index = int3(pMin.x, pMin.y, pMin.z);
            cube4Index = int3(pMin.x - 1, pMin.y, pMin.z);
            break;
        case 2:
            // p1->p2 = x+
            cube1Index = int3(pMin.x, pMin.y - 1, pMin.z - 1);
            cube2Index = int3(pMin.x, pMin.y, pMin.z - 1);
            cube3Index = int3(pMin.x, pMin.y, pMin.z);
            cube4Index = int3(pMin.x, pMin.y - 1, pMin.z);
            break;
        case 3:
            // p1->p2 = x-
            cube1Index = int3(pMin.x, pMin.y - 1, pMin.z - 1);
            cube2Index = int3(pMin.x, pMin.y - 1, pMin.z);
            cube3Index = int3(pMin.x, pMin.y, pMin.z);
            cube4Index = int3(pMin.x, pMin.y, pMin.z - 1);
            break;
        case 4:
            // p1->p2 = z+
            cube1Index = int3(pMin.x - 1, pMin.y - 1, pMin.z);
            cube2Index = int3(pMin.x, pMin.y - 1, pMin.z);
            cube3Index = int3(pMin.x, pMin.y, pMin.z);
            cube4Index = int3(pMin.x - 1, pMin.y, pMin.z);
            break;
        case 5:
            // p1->p2 = z-
            cube1Index = int3(pMin.x - 1, pMin.y - 1, pMin.z);
            cube2Index = int3(pMin.x - 1, pMin.y,     pMin.z);
            cube3Index = int3(pMin.x, pMin.y,         pMin.z);
            cube4Index = int3(pMin.x, pMin.y - 1,     pMin.z);
            break;
    }

    float3 delta = (p2 - p1);
    float length = sqrt(delta.x * delta.x + delta.y * delta.y + delta.z * delta.z);
    float3 normalized = delta / length;

    float3 intx;
    if (d1 == d2 && d1 == 0) intx = (p1 + p2) / (uint)2;
    else if (d1 == 0) intx = decodeDensity(d2) * normalized + p2;
    else if (d2 == 0) intx = decodeDensity(d1) * normalized + p1;
    else {
        float t;
        float density1 = decodeDensity(d1);
        float density2 = decodeDensity(d2);
        if (density1 != density2) {
            t = (-density2) / (density1 - density2);
        }
        intx = (1 - t) * p2 + t * p1;
    }

    //adding a vector.one - probably a hack
    // TODO: why does this work?
    int3 onev = int3(1, 1, 1);
    face.vertexA = cube1Index + onev;
    face.vertexB = cube2Index + onev;
    face.vertexC = cube3Index + onev;
    face.vertexD = cube4Index + onev;
    face.surfaceIntersection = intx;

    return face;
}

int FindBestType(uint x, uint y, uint z) {
    
    for (int k = 0; k < 2; k++) {
        for (int j = 0; j < 2; j++) {
            for (int i = 0; i < 2; i++) {
                if (pointTypes[linearIndex(x + i, y + j, z + k)] != 0) {
                    return pointTypes[linearIndex(x + i, y + j, z + k)];
                }
            }
        }
    }
    return 0;
}

[numthreads(numthreads, numthreads, numthreads)]
void DualContour(int3 id : SV_DispatchThreadID) {

    if (checkBoundsAll(id)) {

        // action per each cell
        int faceType = pointTypes[linearIndex(id.x, id.y, id.z)];
        int faceSignedDist = density[linearIndex(id.x, id.y, id.z)];

        if (isBelowSurface(faceType, faceSignedDist)) {
            for (int dir = 0; dir < 6; dir++) {
                int3 dirV = directionToPos[dir];
                int3 id1 = int3(id.x + dirV.x, id.y + dirV.y, id.z + dirV.z);

                if (checkBoundsAll(id1)) {
                    int density1 = density[linearIndex(id1.x, id1.y, id1.z)];
                    int type1 = pointTypes[linearIndex(id1.x, id1.y, id1.z)];

                    if (!isBelowSurface(type1, density1)) {
                        faces.Append(MakeFace(id, id1, faceSignedDist, density1, dir, faceType));
                    }
                }
            }
        }
    }
}